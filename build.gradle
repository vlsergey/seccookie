plugins {
	id 'java-library'
	id 'maven'
	id 'signing'
	id 'net.researchgate.release' version '2.8.1'
}

group 'io.github.vlsergey'
archivesBaseName = "seccookie"

apply plugin: 'maven'
apply plugin: 'signing'

ext {
	lombokVersion = '1.18.22'
}

compileJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	if (JavaVersion.current().isJava11Compatible()) {
		options.release = 8
	}

	options.compilerArgs << '-parameters'
	options.debugOptions.debugLevel = "source,lines,vars"
	options.encoding = 'UTF-8'
}

java {
	withJavadocJar()
	withSourcesJar()
}

compileTestJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	if (JavaVersion.current().isJava11Compatible()) {
		options.release = 8
	}

	options.compilerArgs << '-parameters'
	options.debugOptions.debugLevel = "source,lines,vars"
	options.encoding = 'UTF-8'
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testAnnotationProcessor group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

	testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
	testImplementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
	testImplementation(platform('org.junit:junit-bom:5.7.2'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

task regenerateReadme {
	doLast {
		def releaseVersion = project.property('release.releaseVersion');
		copy {
			from file( 'README.template.md')
			rename 'README\\.template\\.md', 'README.md'
			into project.projectDir
			expand([ "version": releaseVersion ])
		}

		exec {
			workingDir project.projectDir
			commandLine 'git'
			args 'add', 'README.md'
		}

		exec {
			workingDir project.projectDir
			commandLine 'git'
			args 'commit', '-m', 'Update version in README.md to ' + releaseVersion
		}
	}
}
beforeReleaseBuild.dependsOn regenerateReadme

release {
	failOnCommitNeeded = true
	failOnSnapshotDependencies = true
	failOnUnversionedFiles = true
	failOnUpdateNeeded = true
	pushReleaseVersionBranch = false
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	required { hasProperty('signing.keyId') }
	sign configurations.archives
}

afterReleaseBuild.dependsOn uploadArchives

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment ->
				signing.signPom(deployment)
			}

			repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Secure Cookie Library'
				packaging 'jar'
				description 'Java library for security cookies, client-side pieces of data protected from reading and modifications by client with strong cryptography'
				url 'https://github.com/vlsergey/seccookie'

				scm {
					connection 'scm:git:https://github.com/vlsergey/seccookie.git'
					developerConnection 'scm:svn:https://github.com/vlsergey/seccookie.git'
					url 'https://github.com/vlsergey/seccookie'
				}

				licenses {
					license {
						name 'MIT License'
						url 'https://github.com/vlsergey/seccookie/blob/master/LICENSE'
					}
				}

				developers {
					developer {
						id 'vlsergey'
						name 'Sergey Vladimirov'
						email 'vlsergey@gmail.com'
					}
				}
			}
		}
	}
}
